def apiProperties = rootProject.file('secure/api.properties')

ext {
    config = [
            baseUrl       : "https://api.themoviedb.org/3/",
            apiKey        : getProperty('apiKey', apiProperties),
            poeditorApiKey: getProperty('poeditorApiKey', apiProperties)
    ]
}

buildscript {

    ext {
        pluginsVersions = [
                gradle        : '4.2.2',
                poeditor      : '0.3.3',
                gradleVersions: '0.17.0',
                ktlint        : '8.1.0',
        ]

        versions = [
                kotlin     : '1.5.21',
                coroutines : '1.5.1',
                rxjava     : '2.2.18',
                gson       : '2.8.+',
                annotations: '21.0.1'
        ]

        androidVersions = [
                multidex          : '2.0.1',
                annotation        : '1.2.0',
                koin              : '3.1.2',
                timber            : '4.7.1',
                androidx          : '1.3.+',
                media_androidx    : '1.4.0',
                core_androidx     : '1.6.+',
                androidxLifecycle : '2.3.+',
                recyclerview      : '1.2.+',
                material          : '1.4.+',
                constraint        : '2.0.+',
                retrofit          : '2.9.0',
                networkResponse   : '4.2.2',
                loggingInterceptor: '4.9.0',
                glide             : '4.12.0',
                navigation        : '2.3.+',
                youtubePlayer     : '10.0.5',
                flowbinding       : '1.0.0' // using older version due to Java 11 requirement for newer
        ]

        testVersions = [
                junit   : '4.13.2',
                mockk   : '1.12.0',
                androidx: '2.1.+',
                kluent  : '1.52',
                turbine : '0.5.2'
        ]
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$project.pluginsVersions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.versions.kotlin"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$project.androidVersions.navigation"

        classpath "be.lukin.poeditor:gradle:$project.pluginsVersions.poeditor"
        classpath "com.github.ben-manes:gradle-versions-plugin:$project.pluginsVersions.gradleVersions"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$project.pluginsVersions.ktlint"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        return System.getenv(prop)
    }
}